// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel kPerlin
#pragma kernel kFBM
#pragma kernel kOaxoaSubtractiveFBM
#pragma kernel kDomainWarpingFBM
#pragma kernel kMaxMin
#pragma kernel kBailout
#pragma kernel kHybrid
#pragma kernel kDeriative

#define threadGroupSize 64

struct PointInput
{
	float3 pos;
};

struct PerlinOutput
{
	float val;
};

RWStructuredBuffer<PointInput> points;
RWStructuredBuffer<PerlinOutput> values;
	
static uint permutation4[128] = {
1535746199, 226692954, 895508425, 3775392194, 510076044, 1661505093, 169209893, 2483469847,
1273657591, 1053104640, 3420191838, 537600885, 1478603065, 1463326189, 2289898670, 2940512427,
2252842314, 2786799755, 3885929037, 2061856595, 3867530044, 693922268, 687156791, 915367668,
2705267009, 1230034945, 3146009809, 2836552144, 2189935816, 1453309044, 3329057956, 1073986221,
4209170740, 3389356924, 2121700134, 3562361599, 3812347599, 289017903, 706526646, 3584735199,
43579511, 1185126956, 2607127005, 162278311, 4247197313, 1852596755, 3907023183, 1752218034,
3831625434, 3253871355, 210817774, 4053971903, 3952161617, 1810829049, 534167601, 2641020853,
2966181048, 758282611, 4271244415, 1573776522, 490959582, 2381531160, 1112458112, 3030138327,

1535746199, 226692954, 895508425, 3775392194, 510076044, 1661505093, 169209893, 2483469847,
1273657591, 1053104640, 3420191838, 537600885, 1478603065, 1463326189, 2289898670, 2940512427,
2252842314, 2786799755, 3885929037, 2061856595, 3867530044, 693922268, 687156791, 915367668,
2705267009, 1230034945, 3146009809, 2836552144, 2189935816, 1453309044, 3329057956, 1073986221,
4209170740, 3389356924, 2121700134, 3562361599, 3812347599, 289017903, 706526646, 3584735199,
43579511, 1185126956, 2607127005, 162278311, 4247197313, 1852596755, 3907023183, 1752218034,
3831625434, 3253871355, 210817774, 4053971903, 3952161617, 1810829049, 534167601, 2641020853,
2966181048, 758282611, 4271244415, 1573776522, 490959582, 2381531160, 1112458112, 3030138327
};

static float3 rndvector = float3(28.9017903, 53.7600885, 49.0959582);
	
float fade (float t)
{
	return t * t * t * (t * (t * 6.0 - 15.0) + 10.0);
}
	
float grad (int hash, float x, float y, float z)
{
	int h = hash & 15;
	float u = h<8 ? x : y;
	float v = h<4 ? y : h==12||h==14 ? x : z;
	return ((h&1) == 0 ? u : -u) + ((h&2) == 0 ? v : -v);
}

uint p4i (uint index)
{
	uint u = index / 4;
	uint b = index % 4;
	return (permutation4[u] & (255 << b*8)) >> b*8;
}
	
float perlin (float3 p)
{
	uint3 i = uint3( int(floor(p.x)) & 255, int(floor(p.y)) & 255, int(floor(p.z)) & 255 );

	float3 f = p-floor(p);
	
	float u = fade(f.x);
	float v = fade(f.y);
	float w = fade(f.z);
	
	uint A  = (p4i(i.x) + i.y);
	uint AA = (p4i(A) + i.z);
	uint AB = (p4i(A+1) + i.z);
	uint B  = (p4i( i.x+1 ) + i.y);
	uint BA = (p4i(B) + i.z);
	uint BB = (p4i( B+1 ) + i.z);
	
	return lerp(lerp(lerp(grad(p4i(AA), f.x  , f.y  , f.z   ),  
                          grad(p4i(BA), f.x-1.0, f.y  , f.z   ),
						  u), 
                     lerp(grad(p4i(AB), f.x  , f.y-1.0, f.z   ),  
                          grad(p4i(BB), f.x-1.0, f.y-1.0, f.z   ),
						  u),
					 v),
                lerp(lerp(grad(p4i(AA+1), f.x  , f.y  , f.z-1.0 ),  
                          grad(p4i(BA+1), f.x-1.0, f.y  , f.z-1.0 ),
						  u), 
                     lerp(grad(p4i(AB+1), f.x  , f.y-1.0, f.z-1.0 ),
                          grad(p4i(BB+1), f.x-1.0, f.y-1.0, f.z-1.0 ),
						  u),
				     v),
				w);	
}

float rand (float3 pos)
{
      return 0.5+(frac(sin(dot(pos, float3(12.9898, 78.233, -47.2937))) * 43758.5453))*0.5;
}

// Input: It uses texture coords as the random number seed.
// Output: Random number: [0,1), that is between 0.0 and 0.999999... inclusive.
// Author: Michael Pohoreski
// Copyright: Copyleft 2012 :-)
float rand2 (float3 pos)
{
  // We need irrationals for pseudo randomness.
  // Most (all?) known transcendental numbers will (generally) work.
  const float3 r = float3(
    23.1406926327792690, // e^pi (Gelfond's constant)
    2.6651441426902251,  // 2^sqrt(2) (Gelfond–Schneider constant)
	3.1415926535897932); 
  return frac( cos( fmod( 123456789.0, 1e-7 + 256.0 * dot(pos, r) ) ) );  
}

float fbm (float3 p, int octaves, float persistence)
{
	float v = 0.0;
	float3 t = p;
	
	for (int octave = 0; octave < octaves; octave++)
	{
		v += perlin(t)*pow(2.0, -octave*persistence);
		t *= 2.0;
	}
	
	return v;
}

float oaxoa (float3 p, int octaves, float persistence, int iterations)
{
	float v = abs(fbm(p, octaves, persistence));
	
	for (int i = 0; i < iterations; i++)
		v = abs(v - fbm(p + 10.0*float3(i+1, i+1, i+1), octaves, persistence));
	return v;
}

float domainWarping (float3 p, int octaves, float persistence, int iterations)
{
	float3 v = p;
	float3 offset = float3(0.315, 1.781, 3.917);
	
	for (int i = 0; i < iterations; i++)
	{
		v = float3( fbm(4.0*v + offset*(i+1), octaves, persistence), fbm(4.0*v + 2.0*offset*(i+1), octaves, persistence), fbm(4.0*v + 3.0*offset*(i+1), octaves, persistence) );
	}
	return fbm(p + v, octaves, persistence);
}

float maxmin (float3 p, int octaves, float persistence, int iterations)
{
	float v = 0.0;
	float3 t = p;
	for (int i = 0; i < iterations; i++)
	{
		float f = fbm(t, octaves, persistence);
		if (v < 0.0)
		{
			if (f < v)
				v = f;
		}
		else
		{
			if (f > v)
				v = f;
		}
		t += float3(0.315, 1.781, 3.917);
	}
	return v;
}

float hybrid (float3 p, int octaves, float persistence)
{
	float3 t = p;
	float v = 0.7+perlin(p);//(1.0 - abs(perlin(p)));
	float w = v;
	
	for (int octave = 1; octave < octaves; octave++)
	{
		if (w > 1.0) w = 1.0;
		
		float a = (1.0+perlin(t)) * pow(2.0, -octave*persistence);
		v += w*a;
		w *= a;
		t *= 2.0;
	}
	
	return v;
}

float4 dnoise3f (float3 pos)
{
	float3 i = floor(pos);
	float3 fr = pos-i;	
	
	float3 dr;
	dr.x = 30.0 * fr.x * fr.x * (fr.x * (fr.x-2.0) + 1.0);
	dr.y = 30.0 * fr.y * fr.y * (fr.y * (fr.y-2.0) + 1.0);
	dr.z = 30.0 * fr.z * fr.z * (fr.z * (fr.z-2.0) + 1.0);

	fr.x = fr.x * fr.x * fr.x * (fr.x * (fr.x * 6.0f - 15.0f) + 10.0f);	
	fr.y = fr.y * fr.y * fr.y * (fr.y * (fr.y * 6.0f - 15.0f) + 10.0f);	
	fr.z = fr.z * fr.z * fr.z * (fr.z * (fr.z * 6.0f - 15.0f) + 10.0f);
	
	float a = rand(i);
	float b = rand(i + float3(1.0, 0.0, 0.0) );
	float c = rand(i + float3(0.0, 1.0, 0.0) );
	float d = rand(i + float3(1.0, 1.0, 0.0) );
	float e = rand(i + float3(0.0, 0.0, 1.0) );
	float f = rand(i + float3(1.0, 0.0, 1.0) );
	float g = rand(i + float3(0.0, 1.0, 1.0) );
	float h = rand(i + float3(1.0, 1.0, 1.0) );
	
	float k0 =  a;
	float k1 =  b-a;
	float k2 =  c-a;
	float k3 =  e-a;
	float k4 =  a-b-c+d;
	float k5 =  a-c-e+g;
	float k6 =  a-b-e+f;
	float k7 = -a+b+c-d+e-f-g+h;
	
	return float4( dr.x * (k1 + k4*fr.y + k6*fr.z + k7*fr.y*fr.z),
				   dr.y * (k2 + k5*fr.z + k4*fr.x + k7*fr.z*fr.x),
				   dr.z * (k3 + k6*fr.x + k5*fr.y + k7*fr.x*fr.y),
                   k0 + k1*fr.x + k2*fr.y + k3*fr.z + k4*fr.x*fr.y + k5*fr.y*fr.z + k6*fr.z*fr.x + k7*fr.x*fr.y*fr.z );
}

float deriative_fbm (float3 p, int octaves, float persistence)
{
	float v = 0.0;
	float w = 1.0;
	float3 t = p;
	float3 d = float3(0.0, 0.0, 0.0);
	
	for (int octave = 0; octave < octaves; octave++)
	{
		float4 n = dnoise3f(t);
		d += n;
		w = pow(2.0, -octave*persistence);
		v += w * n.w / (1.0 + d.x*d.x + d.y*d.y + d.z*d.z);
		//w *= 0.8;
		t *= 2.0;
	}
	
	return v;
}

float3 fbmvector (float3 p, int octaves, float persistence)
{
	return float3(fbm(p, octaves, persistence), fbm(p+rndvector, octaves, persistence), fbm(p+2.0*rndvector, octaves, persistence));
}

float bailout (float3 p, int octaves, float persistence, int iterations)
{
	float3 t = p;
	for (int i = 0; i < iterations; i++)
	{
		t += fbmvector(t, octaves, persistence);
	}
	return length(t-p);
}

[numthreads(threadGroupSize,1,1)]
void kPerlin (uint3 id : SV_DispatchThreadID)
{
	values[id.x].val = perlin(points[id.x].pos);
}

[numthreads(threadGroupSize,1,1)]
void kFBM (uint3 id : SV_DispatchThreadID)
{
	values[id.x].val = fbm(points[id.x].pos, 10, 0.75);
}

[numthreads(threadGroupSize,1,1)]
void kOaxoaSubtractiveFBM (uint3 id : SV_DispatchThreadID)
{
	values[id.x].val = oaxoa(points[id.x].pos, 10, 0.75, 10);
}

[numthreads(threadGroupSize,1,1)]
void kDomainWarpingFBM (uint3 id : SV_DispatchThreadID)
{
	values[id.x].val = domainWarping(points[id.x].pos, 10, 0.8, 2);
}

[numthreads(threadGroupSize,1,1)]
void kMaxMin (uint3 id : SV_DispatchThreadID)
{
	values[id.x].val = maxmin(points[id.x].pos, 10, 1.0, 20);
}

[numthreads(threadGroupSize,1,1)]
void kBailout (uint3 id : SV_DispatchThreadID)
{
	values[id.x].val = bailout(points[id.x].pos, 10, 1.0, 5);
}

[numthreads(threadGroupSize,1,1)]
void kHybrid (uint3 id : SV_DispatchThreadID)
{
	values[id.x].val = hybrid(points[id.x].pos, 10, 0.1);
}

[numthreads(threadGroupSize,1,1)]
void kDeriative (uint3 id : SV_DispatchThreadID)
{
	values[id.x].val = deriative_fbm(points[id.x].pos, 10, 0.75);
}